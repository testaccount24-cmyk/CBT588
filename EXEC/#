/*********************************************************************/
/*                               REXX                                */
/*********************************************************************/
/* Purpose: Queue an ISPTART command for batch and ALLOC ISPF DD's   */
/*          if they aren't already allocated in the JCL              */
/*-------------------------------------------------------------------*/
/* Syntax:  $ command                                                */
/*-------------------------------------------------------------------*/
/* Parms: command    - the command to format with all parms          */
/*                                                                   */
/* Notes: Intended to be used in batch - normal IKJEFT01 JCL         */
/*        Any unique DD/DSN's must be added manually to the JCL      */
/*********************************************************************/
/*                      Change Log                                   */
/*                                                                   */
/*  Author       Date      Reason                                    */
/*  --------    ---------  ----------------------------------------- */
/*  R. Zenuk    Sep 1997   Initial Creation                          */
/*  R. Zenuk    10/21/98   Fixed a Comment                           */
/*  R. Zenuk    02/25/00   Add ALLOCs in case ISPF DD's missing      */
/*                                                                   */
/* From: Robert Zenuk (Robzenuk@AOL.COM)                             */
/* Subject: Re: Antwort: Re: How can I run my REXX which edits ...   */
/* Newsgroups: bit.listserv.tsorexx                                  */
/* Date: 2002-02-15 11:05:31 PST                                     */
/*                                                                   */
/*********************************************************************/
/* Set Defaults (add additional DSNs if needed - space delimited)    */
/*********************************************************************/
 ispplib = "'ISP.SISPPENU'"
 ispslib = "'ISP.SISPSENU'"
 ispmlib = "'ISP.SISPMENU'"
 isptlib = "'ISP.SISPTENU'"
 isptxxx = "'"userid()"."mvsvar('SYMDEF','JOBNAME')".ISPPROF'"
 iebcopy = "'SYS1.LINKLIB(IEBCOPY)'"
/*********************************************************************/
/* SYSPRINT messages for IEBCOPY                                     */
/*********************************************************************/
 copyout = 'Z'
/*********************************************************************/
/* Accept input parameters                                           */
/*********************************************************************/
 arg command
/*********************************************************************/
/* Verify parms are present and valid (RC=20 if missing)             */
/*********************************************************************/
 if command ='' then
    do
     say 'Command missing'
     exit(20)
    end
/*********************************************************************/
/* Verify default DSNs exist (RC=16 if any default DSNs are missing) */
/*********************************************************************/
 if sysdsn(ispplib) <> 'OK' then
    do
     say ispplib 'is not cataloged'
     exit(16)
    end
 if sysdsn(ispslib) <> 'OK' then
    do
     say ispslib 'is not cataloged'
     exit(16)
    end
 if sysdsn(ispmlib) <> 'OK' then
    do
     say ispmlib 'is not cataloged'
     exit(16)
    end
 if sysdsn(isptlib) <> 'OK' then
    do
     say isptlib 'is not cataloged'
     exit(16)
    end
 if sysdsn(iebcopy) <> 'OK' then
    do
     say iebcopy 'is missing'
     exit(16)
    end
/*********************************************************************/
/* Check if any ISPF DD's are missing                                */
/*********************************************************************/
 PLIBRC = listdsi('ISPPLIB' 'FILE')
 if PLIBRC <> 0 then "ALLOC F(ISPPLIB) DA("ispplib") SHR REU"
 SLIBRC = listdsi('ISPSLIB' 'FILE')
 if SLIBRC <> 0 then "ALLOC F(ISPSLIB) DA("ispslib") SHR REU"
 MLIBRC = listdsi('ISPMLIB' 'FILE')
 if MLIBRC <> 0 then "ALLOC F(ISPMLIB) DA("ispmlib") SHR REU"
/*********************************************************************/
/* "Temporary" DSN for ISPTLIB, ISPTABL and ISPROF                   */
/*********************************************************************/
 TLIBRC = listdsi('ISPTLIB' 'FILE')
 TABLRC = listdsi('ISPTABL' 'FILE')
 PROFRC = listdsi('ISPPROF' 'FILE')
 if (TLIBRC <> 0 | TABLRC <> 0 | PROFRC <> 0) then
    do
/*********************************************************************/
/* Use IEBCOPY to copy contents of ISPTENU DLIB to ISPTXXX           */
/*********************************************************************/
    "ALLOC F(SYSUT1) DA("isptlib") SHR REU"
     ALLOCRC = RC
     if ALLOCRC <> 0 then say isptlib 'allocation error RC='ALLOCRC
/*********************************************************************/
/* ALLOC ISPTXXX DISP=(NEW,DELETE)                                   */
/*********************************************************************/
    "ALLOC F(SYSUT2) DA("isptxxx") UNI(SYSDA) SPACE(1 1) CYLINDERS",
    "LRECL(80) BLKSIZE(3120) DSORG(PO) DIR(5) NEW DELETE"
     ALLOCRC = RC
     if ALLOCRC <> 0 then say isptlib 'allocation error RC='ALLOCRC
    "ALLOC F(SYSIN) DUMMY"
/*********************************************************************/
/* If SYSPRINT is already in the JCL, use that DD                    */
/*********************************************************************/
     PRINTRC = listdsi('SYSPRINT' 'FILE')
     if PRINTRC <> 0 then "ALLOC F(SYSPRINT) SYSOUT("copyout")"
    "CALL" iebcopy
     COPYRC = RC
     if COPYRC <> 0 then
        do
         say 'IEBCOPY error loading' isptxxx 'RC='COPYRC
         exit(COPYRC)
        end
    end
 if TLIBRC <> 0 then "ALLOC F(ISPTLIB) DA("isptxxx") SHR REU"
 if TABLRC <> 0 then "ALLOC F(ISPTABL) DA("isptxxx") SHR REU"
 if PROFRC <> 0 then "ALLOC F(ISPPROF) DA("isptxxx") SHR REU"
/*********************************************************************/
/* ISPLOG                                                            */
/*********************************************************************/
 LOGRC = listdsi('ISPLOG' 'FILE')
 if LOGRC <> 0 then
   "ALLOC F(ISPLOG) SYSOUT  LRECL(125) BLKSIZE(129) RECFM(V A)"
/*********************************************************************/
/* Queue the command                                                 */
/*********************************************************************/
 queue 'ISPSTART CMD('command')'
/*********************************************************************/
/* exit                                                              */
/*********************************************************************/
 shutdown: exit(0)
