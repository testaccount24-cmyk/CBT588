//jobname JOB (account),userid,CLASS=S,MSGCLASS=Y,REGION=4M
//*
//* ASSEMBLE AND LINK PROGRAM
//ASMH EXEC PGM=ASMA90,REGION=4M,
// PARM='RENT,OBJECT,NODECK,NOXREF,BATCH'
//SYSLIB   DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS3.UTILITY.MACLIB.TEST      ==> hlq.MACLIB
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,2)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//SYSIN    DD *
*---------------------------------------------------------------------*
     TITLE 'WTP parm program'
*---------------------------------------------------------------------*
* History of Changes (last comes first):                              *
*                                                                     *
* 01-03-2003 SVM Fixed a bug...
* 05-24-2002 SVM Created
*.....................................................................*
*                                                                     *
* FUNCTION:                                                           *
*               1) WTO parm string, used for debug only               *
*                                                                     *
* INPUT:        Standard EXEC PARM:                                   *
*               R1-> AL4(PARM)            Addr of length              *
*               PARM   DS  H(K'TEXT)      length of text (halfword)   *
*               TEXT   DS  CL...          text                        *
* OUTPUT:                                                             *
*               Messages on hardcpy log (WTP) containing the TEXT     *
*                                                                     *
* ATTRIBUTES:                                                         *
*               REENTRANT                                             *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
PARM     DSECT
PLEN     DS    H
PTXT     DS    C
*
WRK      DSECT
WRKSAVE  DS    18F                     REGISTER SAVE AREA
MESS     DS    256C
WRKL     EQU   (*-WRK+7)/8*8           WORK AREA LENGTH, words
         EJECT
         REGS
*---------------------------------------------------------------------*
WTP      BEGIN
WRK      WORK  LV=WRKL
         B     START
*
EXIT     FIN
*---------------------------------------------------------------------*
START    L     R2,0(R1)                Addr of PARM
         USING PARM,R2
         LH    R3,PLEN                 Length
         LTR   R3,R3
         BZ    EXIT                    Exit if no text
         MVC   MESS+2(2),WTO0+2        copy WTO0 part 1
         BCTR  R3,0                    len-1 for EX
         EX    R3,MVCPARM              copy text
         LA    R3,5(R3)                len+4 for WTP
         STH   R3,MESS                 copy length
         LA    R4,MESS
         LA    R4,0(R3,R4)             end of text
         MVC   0(4,R4),WTO0+4          copy WTO0 part 2
* Display message:
DISPL    LA    R1,MESS
         SVC   35
         SR    R15,R15
         B     EXIT                    BRANCH TO COMMON EXIT
*=====================================================================*
*        CONSTANTS                                                    *
*---------------------------------------------------------------------*
         DS    0F
MVCPARM  MVC   MESS+4(0),PTXT
WTO0     WTO '',MF=L,ROUTCDE=11
         LTORG
         EJECT
         END
//*
//LKED    EXEC PGM=HEWLKED,PARM='NCAL,MAP,RENT,AC=1,RMOD=ANY,AMOD=31',
//        COND=(8,LE)
//SYSLMOD  DD DISP=SHR,DSN=SYS3.UTILITY.LOADLIB.TEST(WTP)  <== hlq.LOAD
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD DSN=*.ASMH.SYSLIN,DISP=(OLD,PASS)
//*
//GO       EXEC PGM=*.LKED.SYSLMOD,COND=(8,LE),
//   PARM='037E'
//SYSUDUMP DD SYSOUT=*
