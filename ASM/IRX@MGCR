//jobname JOB (account),userid,CLASS=S,MSGCLASS=Y,REGION=4M
//*
//* First modify JOB card and data set names to your environment
//* Assemble and Link program IRX@MGCR
//*
//ASMH EXEC PGM=ASMA90,REGION=4M,
// PARM='RENT,OBJECT,NODECK,NOXREF,BATCH'
//SYSLIB   DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS1.MODGEN
//         DD DISP=SHR,DSN=SYS3.UTILITY.MACLIB.TEST  <== hlq.MACLIB
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,2)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//SYSIN    DD *
*---------------------------------------------------------------------
     TITLE 'IRX@MGCR Linked subroutine to do MGCR macro from REXX'
*---------------------------------------------------------------------
* History of Changes (last comes first):
*
* 02-13-2003 SVM Created
*.....................................................................
*
* FUNCTION:
*               Issue a parameter text as a console
*                  REPLY or START command
*
* Sample use:   REXX: ADDRESS TSO "CALL *(pgmname) '"command"'"
*               JCL:  // EXEC PGM=IRX@MGCR,PARM='command'
*               command must be up to 90 chars long
*
* INPUT:
*               Std EXEC PARM: R1->addr->(len,text)
* OUTPUT:
*               R15 = 0
* ATTRIBUTES:
*               REENTRANT, R=24, A=31, APF-authorised, TSO-authorised
*               (place to load lib that is in LINKLIST
*               and include the pgm name to AUTHPGM list in IKJTSO00)
*---------------------------------------------------------------------
         EJECT
*---------------------------------------------------------------------
IRX@MGCR BEGIN R=24
WRK      WORK  LV=WRKL
         B     START
EXIT     L     R15,RC
         FIN
*---------------------------------------------------------------------
START    DS    0H
         XC    RC(4),RC
         L     R10,0(R1)               - parm addr
         LH    R4,0(R10)               - parm length
         STH   R4,PARMLEN              save
         LA    R3,2(R10)               - parm text
         ST    R3,PARMTXTA             save
*---------------------------------------------------------------------*
*  Get Master Console CNID
*---------------------------------------------------------------------*
         L     R2,16                   CVT
         USING CVT,R2
         L     R3,CVTCUCB              UCMBASE
         USING UCM,R3
         LR    R4,R3
         AH    R4,=H'-4'               GO BACK 4 SPACES
         L     R4,0(R4)                UCM PREFIX
         USING UCMPRFX,R4
         XR    R15,R15
         USING UCMLIST,R14
         IC    R15,UCMID               GET THE CONSOLE ID
         STH   R15,MASTCONS
*---------------------------------------------------------------------*
*   Make PARM for MGCR
*---------------------------------------------------------------------*
         LH    R4,PARMLEN
         L     R3,PARMTXTA
         LTR   R5,R4
         BZ    EXIT                    no parm - no action
         LA    R5,4(R5)                length + 4
         STH   R5,REPLYCMD             clear flags1 and set length
         XC    REPLYCMD+2(2),REPLYCMD+2 clear flags2
         MVC   REPLYTXT(90),#BLANKS    clear REPLYCMD
         BCTR  R5,0                    length-1 for MVC
         EX    R5,MVCPARM              copy text
*
*  Authorised code:
*
         MODESET MODE=SUP,KEY=ZERO
         LH     R0,MASTCONS
         LA     R1,REPLYCMD
         SVC    34                       ISSUE SVC 34 (MGCR)
         MODESET KEY=NZERO,MODE=PROB
*
*  :End of authorised code
*
         B      EXIT
*=====================================================================*
*        CONSTANTS                                                    *
*---------------------------------------------------------------------*
         DS    0F
MVCPARM  MVC   REPLYTXT(1),0(R3)        PARM to REPLYCMD
#BLANKS  DC    CL90' '                  MESSAGE TEXT
         LTORG
         EJECT
*---------------------------------------------------------------------*
*        LOCAL WORK AREA                                              *
*---------------------------------------------------------------------*
         SPACE 1
WRK      DSECT
WRKSAVE  DS    18F                     REGISTER SAVE AREA
RC       DS    F                       RC to return in R15
* command buffer for MGCR: mapped by IEZMGCR DSECT
         DS    0F
REPLYCMD DC    H'0',H'0'     REAL LENGTH, ZERO
REPLYTXT DC    CL90' '
REPLYLEN EQU   *-REPLYCMD          Length of command buffer
*
DSCODES  DS    XL4
MASTCONS DS    H
PARMLEN  DS    H
PARMTXTA DS    A
WRKL     EQU   (*-WRK+7)/8*8           WORK AREA LENGTH, words
         EJECT
*---------------------------------------------------------------------*
*        DSECTS                                                       *
*---------------------------------------------------------------------*
         PUSH  PRINT
         PRINT NOGEN
         REGS
         CVT   DSECT=YES,LIST=NO
         IHAORE
* UCM, UCMPRFX, UCMLIST(?)
UCM      DSECT
         IEECUCM LIST=NO
         IHAWQE
         IHAPSA
         POP   PRINT
         END
//*
//LKED    EXEC PGM=HEWLKED,PARM='CALL,MAP,RENT,AC=1,RMOD=24,AMOD=31',
//        COND=(8,LE)
//SYSLMOD  DD DISP=SHR,DSN=SYS3.UTILITY.LINKLIB(IRX@MGCR)  <== LINKLIB
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD DSN=*.ASMH.SYSLIN,DISP=(OLD,PASS)
//
//* Before running this test, refresh LLA (F LLA,REFRESH) !
//*
//GO      EXEC PGM=IRX@MGCR,COND=(8,LE),
//        PARM='R 10,U'
//
