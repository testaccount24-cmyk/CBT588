//jobname JOB (account),userid,CLASS=S,MSGCLASS=Y,REGION=4M
//*
//* First modify JOB card and data set names to your environment
//* Assemble and Link program/module
//*
//ASMH EXEC PGM=ASMA90,REGION=4M,
// PARM='RENT,OBJECT,NODECK,NOXREF,BATCH'
//SYSLIB   DD DISP=SHR,DSN=SYS3.UTILITY.MACLIB.TEST      <== hlq.MACLIB
//         DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS1.MODGEN
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,2)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//SYSIN    DD *
*---------------------------------------------------------------------*
     TITLE 'SVM0030 Find UCB for VOLSER'
*---------------------------------------------------------------------*
* History of Changes (last comes first):
*
* 05-02-2003 SVM Added 4-digit unit processing
* 02-25-2003 SVM Changed to return UCBTYP+2 in 1st byte of R15
* 11-08-2002 SVM Created
*.....................................................................*
*
* FUNCTION:
*               Find UCB for VOLSER
*
* INPUT:        R1 = A(A(VOLUME),A(UNIT),A(UTYPE))
*               VOLSER DS  CL6
*               UNIT   DS  CL4
*               UTYPE  DS  XL1
* OUTPUT:
*               R15=0 - not found
*               R15>0 - UCB (i.e. C'x37A')
*               1st byte of R15 (x) has UCBTYP+2 byte
* ATTRIBUTES:
*               REENTRANT, AMODE=31, RMODE=ANY
*               Uses UCBSCAN COPY
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
WRK      DSECT
WRKSAVE  DS    18F                     REGISTER SAVE AREA
D        DS    D
VOL      DS    6C
         DS    0D
X100     DS    100C                    UCBSCAN WORKAREA
X48      DS    48X                     COPY OF THE UCB
         UCBSCAN PLISTVER=MAX,MF=(L,UPRM)
WRKL     EQU   (*-WRK+7)/8*8           WORK AREA LENGTH, words
         EJECT
         REGS
*---------------------------------------------------------------------*
SVM0030  BEGIN
WRK      WORK  LV=WRKL,LOC=BELOW
         B     START
EXIT     FIN
*---------------------------------------------------------------------*
START    LR    R8,R1                   save parmlist addr
         L     R1,0(R8)                addr of VOL
         MVC   VOL(6),0(R1)
* Find UCB for VOLSER:
         XC    X100(100),X100          Clear Workarea
         LA    R2,VOL
         LA    R4,X48
         LA    R3,X100
*
 UCBSCAN COPY,WORKAREA=(3),UCBAREA=(4),VOLSER=(2),MF=(E,UPRM,COMPLETE)
*
         LTR   R15,R15
         BZ    FOUND
* not found:
         B     EXIT
         USING UCBOB,R4
FOUND    LA    R4,X48
         L     R1,8(R8)                 addr of UTYPE
         MVC   0(1,R1),UCBTYP+2
         L     R1,4(R8)                 addr of UNIT
         CLC   UCBWGT+1(3),=C'UCB'
         BE    FOURDGTS
         MVC   0(4,R1),UCBWGT           UCB chars
         MVI   0(R1),C'0'
         SR    R15,R15
         B     EXIT
FOURDGTS UNPK  D(5),UCBCHAN(3)          take 4-digit address
         MVC   0(4,R1),D                UCB chars
         SR    R15,R15
         B     EXIT
*=====================================================================*
*        CONSTANTS
*---------------------------------------------------------------------*
         DS    0F
         LTORG
         EJECT
         DSECT
         IEFUCBOB                      UCB MACRO ID
*        CVT   DSECT=YES
         EJECT
         END
//*
//LKED    EXEC PGM=HEWLKED,
//        PARM='NCAL,MAP,RENT,AC=1,RMOD=ANY,AMOD=31',
//        COND=(8,LE)
//SYSLMOD  DD DISP=SHR,DSN=SYS3.UTILITY.LOADLIB.TEST(SVM0030) <= *.LOAD
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD DSN=*.ASMH.SYSLIN,DISP=(OLD,PASS)
//
