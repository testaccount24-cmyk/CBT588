.* SAY macro issues WTP to dump the value while debug.
.*    it is not re-entrant!
.* SAY TEXT,VALUE,LEN
.*    TEXT must be 'TEXT' or omitted, example: 'DATE = '
.*    VALUE may be either a name or (R) loaded with address
.*    examples: (R2), DATA122
.*    LEN is optional when VALUE is a name. Must be a numeric length
.*        of the VALUE. Required when VALUE is (r)
         MACRO
&NAME    SAY     &TEXT,&VALUE,&LEN
         GBLA    &SAY#
&SAY#    SETA    &SAY#+1
&NAME    DS    0H
&NAM     SETC    'SAY&SAY#'
.NAM     AIF     ('&TEXT' NE '').C00
.* -- empty TEXT - set length to 0
&TLEN    SETA    0
         AGO     .C0
.* -- text is not empty, must be in quotes:
.C00     ANOP
&TLEN    SETA    K'&TEXT-2             Length without quotes
&TXT     SETC    '&TEXT'
.C0      AIF     ('&VALUE' NE '').V00
.* -- empty Value - set length to 0
&PLEN    SETA    0
         AGO     .C1
.* -- value in not empty:
.V00     ANOP
&VAL     SETC    '&VALUE'
         AIF     ('&LEN' EQ '').NOLEN
.* -- Len present - check if Value is (register):
&PLEN    SETA    &LEN
         AIF     ('&VALUE'(1,1) NE '(').C1
.* -- value as a (register):
&VAL     SETC    '0&VALUE'
         AGO     .C1
.NOLEN   ANOP
&PLEN    SETA    L'&VALUE
.* -- Generate code:
.C1      ANOP
&MLEN    SETA    &TLEN+&PLEN
         AIF     ('&MLEN' EQ '0').END
         AIF     (&TLEN EQ 0).C11
         MVC     &NAM+8(&TLEN),=C&TXT
.C11     AIF     (&PLEN EQ 0).C12
         MVC     &NAM+8+&TLEN.(&PLEN),&VAL
.C12     ANOP
         CNOP    0,4
         MVI     *+9,&MLEN+4
&NAM     WTO     L&MLEN' ',ROUTCDE=11
.END     ANOP
         MEND
